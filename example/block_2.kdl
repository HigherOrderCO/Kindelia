// This block defines and uses a stateful function.

// Creates a Counter function with 2 actions:
ctr {Inc} // action that increments the counter
ctr {Get} // action that returns the counter
fun (Counter action) {

  // increments the counter
  (Counter {Inc}) =
    !take x        // loads the state and assigns it to 'x'
    !save (+ x #1) // overwrites the state as 'x + 1'
    !done #0       // returns 0

  // returns the counter
  (Counter {Get}) =
    !load x // loads the state
    !done x // returns it

// initial state is 0
} with {
  #0
}

// Increments the Counter's state 3 times
run {
  !call ~ 'Counter' [{Inc}]
  !call ~ 'Counter' [{Inc}]
  !call ~ 'Counter' [{Inc}]
  !done #0
}

// Prints the Counter's state
run {
  !call x 'Counter' [{Get}]
  !done x
}
