// This block defines and uses some pure functions.

// Declares the constructors of a binary tree.
ctr {Leaf value}
ctr {Branch left right}

// Declares a pure function that sums a tree
fun (Sum tree) {
  (Sum {Leaf x})     = x
  (Sum {Branch a b}) = (+ (Sum a) (Sum b))
}

// Sums a tree with 4 numbers
run {
  !done (Sum {Branch
    {Branch {Leaf #1} {Leaf #2}}
    {Branch {Leaf #3} {Leaf #4}}})
}

// Generates a tree with 2^n copies of the number #1
fun (Gen depth) {
  (Gen #0) =
    {Leaf #1}
  (Gen x) =
    dup x0 x1 = x;
    {Branch (Gen (- x0 #1)) (Gen (- x1 #1))}
}

// Sums a huge tree, with 2^21 copies of the number #1
run {
  !done (Sum (Gen #20))
}
